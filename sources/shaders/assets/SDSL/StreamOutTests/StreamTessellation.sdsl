// DSMain(ExpectedPrimitiveCount=1, stream.EXTRA_COLOR=(0.498 0.498 0.498 0.498), cbuffer.Test=(TessFactors=(1,1,1,1)))
// DSMain(ExpectedPrimitiveCount=7, stream.EXTRA_COLOR=(0.498 0.498 0.498 0.498), cbuffer.Test=(TessFactors=(1,1,1,2)))
// DSMain(ExpectedPrimitiveCount=13, stream.EXTRA_COLOR=(0.498 0.498 0.498 0.498), cbuffer.Test=(TessFactors=(2,2,2,1)))

namespace Stride.Shaders.Tests;

shader StreamTessellation
{
    stream float4 Position : POSITION;
    stream float4 ShadingPosition : SV_Position;
    stream float4 ColorTarget : SV_Target0;
    stream float4 ExtraColor : EXTRA_COLOR;
    stream float4 ExtraColor2;
    
    patchstream float Edges[3] : SV_TessFactor;
    patchstream float Inside : SV_InsideTessFactor;

    cbuffer Test
    {
        float4 TessFactors;
    }

    void VSMain()
    {
        streams.ShadingPosition = streams.Position;
        streams.ExtraColor2 = streams.ExtraColor;
    }
    
    void HSConstantMain(InputPatch<Input, 3> input, const OutputPatch<Input2, 3> output, out Constants constants)
    {
        constants.Edges[0] = TessFactors.x;
        constants.Edges[1] = TessFactors.y;
        constants.Edges[2] = TessFactors.z;
        constants.Inside = TessFactors.w;
    }
    
    [domain("tri")]
    [partitioning("fractional_odd")]
    [outputtopology("triangle_cw")]
    [outputcontrolpoints(3)]
    [patchconstantfunc("HSConstantMain")]
    void HSMain(InputPatch<Input, 3> input, out Output output, uint uCPID : SV_OutputControlPointID) 
    {
        streams = input[uCPID];

        output = streams;
    }

    [domain("tri")]
    void DSMain(const OutputPatch<Input, 3> input, out Output output, in Constants constants, float3 f3BarycentricCoords : SV_DomainLocation)
    {
        float fU = f3BarycentricCoords.x;
        float fV = f3BarycentricCoords.y;
        float fW = f3BarycentricCoords.z;

        streams = input[0] * fU + input[1] * fV + input[2] * fW;
        output = streams;
    } 

    void PSMain()
    {
        streams.ColorTarget = streams.ExtraColor2;
    }
}
