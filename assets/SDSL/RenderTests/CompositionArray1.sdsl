// PSMain(ExpectedResult=#1A250000)

namespace Stride.Shaders.Tests;

shader CompositionBase
{
	float Compute()
	{
		return 3.0 / 255.0;
	}
};
 
shader CompositionShaderA : CompositionBase
{
	override float Compute()
	{
		return 5.0 / 255.0;
	}
};
 
shader CompositionShaderB : CompositionBase
{
	override float Compute()
	{
		return base.Compute() + 13.0 / 255.0;
	}
};

shader CompositionTestArray
{
	stage compose CompositionBase CompsInherited[];
}
 
shader CompositionTest : CompositionTestArray
{
    stream float4 ColorTarget : SV_Target0;

	stage compose CompositionBase CompsLocal[];

	void PSMain()
	{
		streams.ColorTarget = 0.0;
		foreach(var comp in CompsLocal)
		{
			streams.ColorTarget.x += comp.Compute();
		}
		foreach(var comp in CompsInherited)
		{
			streams.ColorTarget.y += comp.Compute();
		}
	}
};

effect CompositionArray1
{
	mixin CompositionTest;
	mixin compose CompsLocal += CompositionShaderA;
	mixin compose CompsLocal += CompositionShaderA;
	mixin compose CompsLocal += CompositionShaderB;
	mixin compose CompsInherited += CompositionShaderA;
	mixin compose CompsInherited += CompositionShaderB;
	mixin compose CompsInherited += CompositionShaderB;
}